options:
    
    # 资源包的URL下载链接
    url: "https://rymc.ltd/survival/pack/2.0"

    # 资源包的SHA-1值
    hash: "665ad5d064b5d00c6728b041bf172449a830ef81"

    # 资源包支持的最低协议版本
    version: 498


on join:
    player is not from floodgate
    protocol version of player >= {@version}
    send the resource pack from {@url} with hash {@hash} to the player

command /loadpack:
    trigger:
        if player is from floodgate:
            send "§f[§6服务器§f] 基岩版玩家不支持此功能" to player
        else if protocol version of player < {@version}:
            send "§f[§6服务器§f] 你的游戏版本过低,不支持此功能" to player
        else:
            send the resource pack from {@url} with hash {@hash} to the player
            send "§f[§6服务器§f] 已发送资源包加载请求" to player

on resource pack request response:
    if the resource pack was declined:
        send "§f[§6服务器§f] 服务器资源包加载被禁用" to player
        send "§f[§6服务器§f] 请将服务器列表中§c编辑§f页面的§c服务器资源包§f选项设置为启用" to player
    else if the resource pack was failed to download:
        send "§f[§6服务器§f] 服务器资源包下载失败,使用指令§e/loadpack§f再次加载资源包" to player
